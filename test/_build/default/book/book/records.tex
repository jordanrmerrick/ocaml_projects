\hypertarget{records}{%
\section{Records}\label{records}}

One of OCaml's best features is its concise and expressive system for
declaring new data types. \emph{Records} are a key element of that
system. We discussed records briefly in
\href{guided-tour.html\#a-guided-tour}{A Guided Tour}, but this chapter
will go into more depth, covering more of the technical details, as well
as providing advice on how to use records effectively in your software
designs.

A record represents a collection of values stored together as one, where
each component is identified by a different field name. The basic syntax
for a record type declaration is as follows:
\index{records/basic syntax for}

\begin{lstlisting}
type <record-name> =
    { <field> : <type>;
      <field> : <type>;
      ...
    }
\end{lstlisting}

Note that record field names must start with a lowercase letter.

Here's a simple example: a \passthrough{\lstinline!service\_info!}
record that represents an entry from the
\passthrough{\lstinline!/etc/services!} file on a typical Unix system.
That file is used for keeping track of the well-known port and protocol
name for protocols such as FTP or SSH. Note that we're going to open
\passthrough{\lstinline!Core!} in this example rather than
\passthrough{\lstinline!Base!}, since we're using the Unix API, which
you need \passthrough{\lstinline!Core!} for.

\begin{lstlisting}[language=Caml]
# open Core
# type service_info =
    { service_name : string;
      port         : int;
      protocol     : string;
    }
type service_info = { service_name : string; port : int; protocol : string; }
\end{lstlisting}

We can construct a \passthrough{\lstinline!service\_info!} just as
easily as we declared its type. The following function tries to
construct such a record given as input a line from
\passthrough{\lstinline!/etc/services!} file. To do this, we'll use
\passthrough{\lstinline!Re!}, a regular expression engine for OCaml. If
you don't know how regular expressions work, you can just think of them
as a simple pattern language you can use for parsing a string.

\begin{lstlisting}[language=Caml]
# #require "re"
# let service_info_of_string line =
    let matches =
      Re.exec (Re.Posix.compile_pat "([a-zA-Z]+)[ \t]+([0-9]+)/([a-zA-Z]+)") line
    in
    { service_name = Re.Group.get matches 1;
      port = Int.of_string (Re.Group.get matches 2);
      protocol = Re.Group.get matches 3;
    }
val service_info_of_string : string -> service_info = <fun>
\end{lstlisting}

We can construct a concrete record by calling the function on a line
from the file.

\begin{lstlisting}[language=Caml]
# let ssh = service_info_of_string "ssh 22/udp # SSH Remote Login Protocol"
val ssh : service_info = {service_name = "ssh"; port = 22; protocol = "udp"}
\end{lstlisting}

You might wonder how the compiler inferred that our function returns a
value of type \passthrough{\lstinline!service\_info!}. In this case, the
compiler bases its inference on the field names used in constructing the
record. That inference is most straightforward when each field name
belongs to only one record type. We'll discuss later in the chapter what
happens when field names are shared across different record types.

Once we have a record value in hand, we can extract elements from the
record field using dot notation:

\begin{lstlisting}[language=Caml]
# ssh.port
- : int = 22
\end{lstlisting}

When declaring an OCaml type, you always have the option of
parameterizing it by a polymorphic type. Records are no different in
this regard. As an example, here's a type that represents an arbitrary
item tagged with a line number.

\begin{lstlisting}[language=Caml]
# type 'a with_line_num = { item: 'a; line_num: int }
type 'a with_line_num = { item : 'a; line_num : int; }
\end{lstlisting}

We can then write polymorphic functions that operate over this
parameterized type. For example, this function takes a file and parses
it as a series of lines, using the provided function for parsing each
individual line.

\begin{lstlisting}[language=Caml]
# let parse_lines parse file_contents =
    let lines = String.split ~on:'\n' file_contents in
    List.mapi lines ~f:(fun line_num line ->
      { item = parse line;
        line_num = line_num + 1;
      })
val parse_lines : (string -> 'a) -> string -> 'a with_line_num list = <fun>
\end{lstlisting}

We can then use this function for parsing a snippet of a real
\passthrough{\lstinline!/etc/services!} file.

\begin{lstlisting}[language=Caml]
# parse_lines service_info_of_string
    "rtmp              1/ddp     # Routing Table Maintenance Protocol
     tcpmux            1/udp     # TCP Port Service Multiplexer
     tcpmux            1/tcp     # TCP Port Service Multiplexer"
- : service_info with_line_num list =
[{item = {service_name = "rtmp"; port = 1; protocol = "ddp"}; line_num = 1};
 {item = {service_name = "tcpmux"; port = 1; protocol = "udp"}; line_num = 2};
 {item = {service_name = "tcpmux"; port = 1; protocol = "tcp"}; line_num = 3}]
\end{lstlisting}

The polymorphism lets us use the same function when parsing a different
format, like this function for parsing a file containing an integer on
every line.

\begin{lstlisting}[language=Caml]
# parse_lines Int.of_string "1\n10\n100\n1000"
- : int with_line_num list =
[{item = 1; line_num = 1}; {item = 10; line_num = 2};
 {item = 100; line_num = 3}; {item = 1000; line_num = 4}]
\end{lstlisting}

\hypertarget{patterns-and-exhaustiveness}{%
\subsection{Patterns and
Exhaustiveness}\label{patterns-and-exhaustiveness}}

Another way of getting information out of a record is by using a pattern
match, as shown in the following function.\index{pattern
matching/and exhaustiveness}\index{records/patterns and exhaustiveness
in}

\begin{lstlisting}[language=Caml]
# let service_info_to_string { service_name = name; port = port; protocol = prot  } =
    sprintf "%s %i/%s" name port prot
val service_info_to_string : service_info -> string = <fun>
# service_info_to_string ssh
- : string = "ssh 22/udp"
\end{lstlisting}

Note that the pattern we used had only a single case, rather than using
several cases separated by \passthrough{\lstinline!|!}'s. We needed only
one pattern because record patterns are \emph{irrefutable}, meaning that
a record pattern match will never fail at runtime. That's because the
set of fields available in a record is always the same. In general,
patterns for types with a fixed structure, like records and tuples, are
irrefutable, unlike types with variable structures like lists and
variants.\index{irrefutable patterns}\index{datatypes/fixed vs.  variable
structure of}

Another important characteristic of record patterns is that they don't
need to be complete; a pattern can mention only a subset of the fields
in the record. This can be convenient, but it can also be error prone.
In particular, this means that when new fields are added to the record,
code that should be updated to react to the presence of those new fields
will not be flagged by the compiler.

As an example, imagine that we wanted to change our
\passthrough{\lstinline!service\_info!} record so that it preserves
comments. We can do this by providing a new definition of
\passthrough{\lstinline!service\_info!} that includes a
\passthrough{\lstinline!comment!} field:

\begin{lstlisting}[language=Caml]
# type service_info =
    { service_name : string;
      port         : int;
      protocol     : string;
      comment      : string option;
    }
type service_info = {
  service_name : string;
  port : int;
  protocol : string;
  comment : string option;
}
\end{lstlisting}

The code for \passthrough{\lstinline!service\_info\_to\_string!} would
continue to compile without change. But in this case, we should probably
update the code so that the generated string includes the comment if
it's there. It would be nice if the type system would warn us that we
should consider updating the function.

Happily, OCaml offers an optional warning for missing fields in record
patterns. With that warning turned on (which you can do in the toplevel
by typing \passthrough{\lstinline!\#warnings "+9"!}), the compiler will
indeed warn
us.\index{errors/compiler warnings}\index{code compilers/warning
enable/disable}\index{errors/missing field
warnings}\index{records/missing field warnings}

\begin{lstlisting}[language=Caml]
# #warnings "+9"
# let service_info_to_string { service_name = name; port = port; protocol = prot  } =
    sprintf "%s %i/%s" name port prot
Line 1, characters 28-82:
Warning 9: the following labels are not bound in this record pattern:
comment
Either bind these labels explicitly or add '; _' to the pattern.
val service_info_to_string : service_info -> string = <fun>
\end{lstlisting}

We can disable the warning for a given pattern by explicitly
acknowledging that we are ignoring extra fields. This is done by adding
an underscore to the pattern:

\begin{lstlisting}[language=Caml]
# let service_info_to_string { service_name = name; port = port; protocol = prot; _ } =
    sprintf "%s %i/%s" name port prot
val service_info_to_string : service_info -> string = <fun>
\end{lstlisting}

It's a good idea to enable the warning for incomplete record matches and
to explicitly disable it with an \passthrough{\lstinline!\_!} where
necessary.

::: \{.allow\_break data-type=note\} \#\#\# Compiler Warnings

The OCaml compiler is packed full of useful warnings that can be enabled
and disabled separately. These are documented in the compiler itself, so
we could have found out about warning 9 as follows:

\begin{lstlisting}[language=bash]
$ ocaml -warn-help | egrep '\b9\b'
  9 Missing fields in a record pattern.
  R Alias for warning 9.
\end{lstlisting}

You can think of OCaml's warnings as a powerful set of optional static
analysis tools. They're enormously helpful in catching all sorts of
bugs, and you should enable them in your build environment. You don't
typically enable all warnings, but the defaults that ship with the
compiler are pretty good.

The warnings used for building the examples in this book are specified
with the following flag:
\passthrough{\lstinline!-w @A-4-33-40-41-42-43-34-44!}.

The syntax of \passthrough{\lstinline!-w!} can be found by running
\passthrough{\lstinline!ocaml -help!}, but this particular invocation
turns on all warnings as errors, disabling only the numbers listed
explicitly after the \passthrough{\lstinline!A!}.

Treating warnings as errors (i.e., making OCaml fail to compile any code
that triggers a warning) is good practice, since without it, warnings
are too often ignored during development. When preparing a package for
distribution, however, this is a bad idea, since the list of warnings
may grow from one release of the compiler to another, and so this may
lead your package to fail to compile on newer compiler releases. :::

\hypertarget{field-punning}{%
\subsection{Field Punning}\label{field-punning}}

When the name of a variable coincides with the name of a record field,
OCaml provides some handy syntactic shortcuts. For example, the pattern
in the following function binds all of the fields in question to
variables of the same name. This is called \emph{field
punning}:\index{fields/field punning}\index{records/field punning in}

\begin{lstlisting}[language=Caml]
# let service_info_to_string { service_name; port; protocol; comment } =
    let base = sprintf "%s %i/%s" service_name port protocol in
    match comment with
    | None -> base
    | Some text -> base ^ " #" ^ text
val service_info_to_string : service_info -> string = <fun>
\end{lstlisting}

Field punning can also be used to construct a record. Consider the
following updated version of
\passthrough{\lstinline!service\_info\_of\_string!}.\index{records/construction of}

\begin{lstlisting}[language=Caml]
# let service_info_of_string line =
    (* first, split off any comment *)
    let (line,comment) =
      match String.rsplit2 line ~on:'#' with
      | None -> (line,None)
      | Some (ordinary,comment) -> (ordinary, Some comment)
    in
    (* now, use a regular expression to break up the service definition *)
    let matches =
      Re.exec (Re.Posix.compile_pat "([a-zA-Z]+)[ \t]+([0-9]+)/([a-zA-Z]+)") line
    in
    let service_name = Re.Group.get matches 1 in
    let port = Int.of_string (Re.Group.get matches 2) in
    let protocol = Re.Group.get matches 3 in
    { service_name; port; protocol; comment }
val service_info_of_string : string -> service_info = <fun>
\end{lstlisting}

In the preceding code, we defined variables corresponding to the record
fields first, and then the record declaration itself simply listed the
fields that needed to be included. You can take advantage of both field
punning and label punning when writing a function for constructing a
record from labeled arguments:\index{label
punning}\index{records/label punning in}

\begin{lstlisting}[language=Caml]
# let create_service_info ~service_name ~port ~protocol ~comment =
    { service_name; port; protocol; comment }
val create_service_info :
  service_name:string ->
  port:int -> protocol:string -> comment:string option -> service_info =
  <fun>
\end{lstlisting}

This is considerably more concise than what you would get without
punning:

\begin{lstlisting}[language=Caml]
# let create_service_info
        ~service_name:service_name ~port:port
        ~protocol:protocol ~comment:comment =
    { service_name = service_name;
      port = port;
      protocol = protocol;
      comment = comment;
    }
val create_service_info :
  service_name:string ->
  port:int -> protocol:string -> comment:string option -> service_info =
  <fun>
\end{lstlisting}

Together, field and label punning encourage a style where you propagate
the same names throughout your codebase. This is generally good
practice, since it encourages consistent naming, which makes it easier
to navigate the source.

\hypertarget{reusing-field-names}{%
\subsection{Reusing Field Names}\label{reusing-field-names}}

Defining records with the same field names can be problematic. As a
simple example, let's consider a collection of types representing the
protocol of a logging server.\index{fields/reusing field
names}\index{records/reusing field names}

We'll describe three message types:
\passthrough{\lstinline!log\_entry!},
\passthrough{\lstinline!heartbeat!}, and
\passthrough{\lstinline!logon!}. The
\passthrough{\lstinline!log\_entry!} message is used to deliver a log
entry to the server; the \passthrough{\lstinline!logon!} message is sent
when initiating a connection and includes the identity of the user
connecting and credentials used for authentication; and the
\passthrough{\lstinline!heartbeat!} message is periodically sent by the
client to demonstrate to the server that the client is alive and
connected. All of these messages include a session ID and the time the
message was generated.

\begin{lstlisting}[language=Caml]
# type log_entry =
    { session_id: string;
      time: Time_ns.t;
      important: bool;
      message: string;
    }
  type heartbeat =
    { session_id: string;
      time: Time_ns.t;
      status_message: string;
    }
  type logon =
    { session_id: string;
      time: Time_ns.t;
      user: string;
      credentials: string;
    }
type log_entry = {
  session_id : string;
  time : Time_ns.t;
  important : bool;
  message : string;
}
type heartbeat = {
  session_id : string;
  time : Time_ns.t;
  status_message : string;
}
type logon = {
  session_id : string;
  time : Time_ns.t;
  user : string;
  credentials : string;
}
\end{lstlisting}

Reusing field names can lead to some ambiguity. For example, if we want
to write a function to grab the \passthrough{\lstinline!session\_id!}
from a record, what type will it have?

\begin{lstlisting}[language=Caml]
# let get_session_id t = t.session_id
val get_session_id : logon -> string = <fun>
\end{lstlisting}

In this case, OCaml just picks the most recent definition of that record
field. We can force OCaml to assume we're dealing with a different type
(say, a \passthrough{\lstinline!heartbeat!}) using a type annotation:

\begin{lstlisting}[language=Caml]
# let get_heartbeat_session_id (t:heartbeat) = t.session_id
val get_heartbeat_session_id : heartbeat -> string = <fun>
\end{lstlisting}

While it's possible to resolve ambiguous field names using type
annotations, the ambiguity can be a bit confusing. Consider the
following functions for grabbing the session ID and status from a
heartbeat:

\begin{lstlisting}[language=Caml]
# let status_and_session t = (t.status_message, t.session_id)
val status_and_session : heartbeat -> string * string = <fun>
# let session_and_status t = (t.session_id, t.status_message)
Line 1, characters 45-59:
Error: This expression has type logon
       The field status_message does not belong to type logon
# let session_and_status (t:heartbeat) = (t.session_id, t.status_message)
val session_and_status : heartbeat -> string * string = <fun>
\end{lstlisting}

Why did the first definition succeed without a type annotation and the
second one fail? The difference is that in the first case, the
type-checker considered the \passthrough{\lstinline!status\_message!}
field first and thus concluded that the record was a
\passthrough{\lstinline!heartbeat!}. When the order was switched, the
\passthrough{\lstinline!session\_id!} field was considered first, and so
that drove the type to be considered to be a
\passthrough{\lstinline!logon!}, at which point
\passthrough{\lstinline!t.status\_message!} no longer made sense.

We can avoid this ambiguity altogether, either by using nonoverlapping
field names or by putting different record types in different modules.
Indeed, packing types into modules is a broadly useful idiom (and one
used quite extensively by \passthrough{\lstinline!Core!}), providing for
each type a namespace within which to put related values. When using
this style, it is standard practice to name the type associated with the
module \passthrough{\lstinline!t!}. Using this style we would write:

\begin{lstlisting}[language=Caml]
# module Log_entry = struct
    type t =
      { session_id: string;
        time: Time_ns.t;
        important: bool;
        message: string;
      }
  end
  module Heartbeat = struct
    type t =
      { session_id: string;
        time: Time_ns.t;
        status_message: string;
      }
  end
  module Logon = struct
    type t =
      { session_id: string;
        time: Time_ns.t;
        user: string;
        credentials: string;
      }
  end
module Log_entry :
  sig
    type t = {
      session_id : string;
      time : Time_ns.t;
      important : bool;
      message : string;
    }
  end
module Heartbeat :
  sig
    type t = {
      session_id : string;
      time : Time_ns.t;
      status_message : string;
    }
  end
module Logon :
  sig
    type t = {
      session_id : string;
      time : Time_ns.t;
      user : string;
      credentials : string;
    }
  end
\end{lstlisting}

Now, our log-entry-creation function can be rendered as follows:

\begin{lstlisting}[language=Caml]
# let create_log_entry ~session_id ~important message =
    { Log_entry.time = Time_ns.now ();
      Log_entry.session_id;
      Log_entry.important;
      Log_entry.message
    }
val create_log_entry :
  session_id:string -> important:bool -> string -> Log_entry.t = <fun>
\end{lstlisting}

The module name \passthrough{\lstinline!Log\_entry!} is required to
qualify the fields, because this function is outside of the
\passthrough{\lstinline!Log\_entry!} module where the record was
defined. OCaml only requires the module qualification for one record
field, however, so we can write this more concisely. Note that we are
allowed to insert whitespace between the module path and the field name:

\begin{lstlisting}[language=Caml]
# let create_log_entry ~session_id ~important message =
    { Log_entry.
      time = Time_ns.now (); session_id; important; message }
val create_log_entry :
  session_id:string -> important:bool -> string -> Log_entry.t = <fun>
\end{lstlisting}

Earlier, we saw that you could help OCaml understand which record field
was intended by adding a type annotation. We can use that here to make
the example even more concise.

\begin{lstlisting}[language=Caml]
# let create_log_entry ~session_id ~important message : Log_entry.t =
    { time = Time_ns.now (); session_id; important; message }
val create_log_entry :
  session_id:string -> important:bool -> string -> Log_entry.t = <fun>
\end{lstlisting}

This is not restricted to constructing a record; we can use the same
approaches when pattern matching:

\begin{lstlisting}[language=Caml]
# let message_to_string { Log_entry.important; message; _ } =
    if important then String.uppercase message else message
val message_to_string : Log_entry.t -> string = <fun>
\end{lstlisting}

When using dot notation for accessing record fields, we can qualify the
field by the module as well.

\begin{lstlisting}[language=Caml]
# let is_important t = t.Log_entry.important
val is_important : Log_entry.t -> bool = <fun>
\end{lstlisting}

The syntax here is a little surprising when you first encounter it. The
thing to keep in mind is that the dot is being used in two ways: the
first dot is a record field access, with everything to the right of the
dot being interpreted as a field name; the second dot is accessing the
contents of a module, referring to the record field
\passthrough{\lstinline!important!} from within the module
\passthrough{\lstinline!Log\_entry!}. The fact that
\passthrough{\lstinline!Log\_entry!} is capitalized and so can't be a
field name is what disambiguates the two uses.

Qualifying a record field by the module it comes from can be awkward.
Happily, OCaml doesn't require that the record field be qualified if it
can otherwise infer the type of the record in question. In particular,
we can rewrite the above declarations by adding type annotations and
removing the module qualifications.

\begin{lstlisting}[language=Caml]
# let create_log_entry ~session_id ~important message : Log_entry.t =
    { time = Time_ns.now (); session_id; important; message }
val create_log_entry :
  session_id:string -> important:bool -> string -> Log_entry.t = <fun>
# let message_to_string ({ important; message; _ } : Log_entry.t) =
    if important then String.uppercase message else message
val message_to_string : Log_entry.t -> string = <fun>
# let is_important (t:Log_entry.t) = t.important
val is_important : Log_entry.t -> bool = <fun>
\end{lstlisting}

This feature of the language, known by the somewhat imposing name of
\emph{type-directed constructor disambiguation}, applies to variant
constructors as well as record fields, as we'll see in
\href{variants.html\#variants}{Variants}.

For functions defined within the module where a given record is defined,
the module qualification goes away entirely.

\hypertarget{functional-updates}{%
\subsection{Functional Updates}\label{functional-updates}}

Fairly often, you will find yourself wanting to create a new record that
differs from an existing record in only a subset of the fields. For
example, imagine our logging server had a record type for representing
the state of a given client, including when the last heartbeat was
received from that client. The following defines a type for representing
this information, as well as a function for updating the client
information when a new heartbeat arrives:\index{functional
updates}\index{records/functional updates to}

\begin{lstlisting}[language=Caml]
# type client_info =
    { addr: Unix.Inet_addr.t;
      port: int;
      user: string;
      credentials: string;
      last_heartbeat_time: Time_ns.t;
  }
type client_info = {
  addr : Unix.inet_addr;
  port : int;
  user : string;
  credentials : string;
  last_heartbeat_time : Time_ns.t;
}
# let register_heartbeat t hb =
    { addr = t.addr;
      port = t.port;
      user = t.user;
      credentials = t.credentials;
      last_heartbeat_time = hb.Heartbeat.time;
  }
val register_heartbeat : client_info -> Heartbeat.t -> client_info = <fun>
\end{lstlisting}

This is fairly verbose, given that there's only one field that we
actually want to change, and all the others are just being copied over
from \passthrough{\lstinline!t!}. We can use OCaml's \emph{functional
update} syntax to do this more tersely. The syntax of a functional
update is as follows:

\begin{lstlisting}
{ <record> with <field> = <value>;
      <field> = <value>;
      ...
}
\end{lstlisting}

The purpose of the functional update is to create a new record based on
an existing one, with a set of field changes layered on top.

Given this, we can rewrite \passthrough{\lstinline!register\_heartbeat!}
more concisely:

\begin{lstlisting}[language=Caml]
# let register_heartbeat t hb =
  { t with last_heartbeat_time = hb.Heartbeat.time }
val register_heartbeat : client_info -> Heartbeat.t -> client_info = <fun>
\end{lstlisting}

Functional updates make your code independent of the identity of the
fields in the record that are not changing. This is often what you want,
but it has downsides as well. In particular, if you change the
definition of your record to have more fields, the type system will not
prompt you to reconsider whether your code needs to change to
accommodate the new fields. Consider what happens if we decided to add a
field for the status message received on the last heartbeat:

\begin{lstlisting}[language=Caml]
# type client_info =
    { addr: Unix.Inet_addr.t;
      port: int;
      user: string;
      credentials: string;
      last_heartbeat_time: Time_ns.t;
      last_heartbeat_status: string;
  }
type client_info = {
  addr : Unix.inet_addr;
  port : int;
  user : string;
  credentials : string;
  last_heartbeat_time : Time_ns.t;
  last_heartbeat_status : string;
}
\end{lstlisting}

The original implementation of
\passthrough{\lstinline!register\_heartbeat!} would now be invalid, and
thus the compiler would effectively warn us to think about how to handle
this new field. But the version using a functional update continues to
compile as is, even though it incorrectly ignores the new field. The
correct thing to do would be to update the code as follows:

\begin{lstlisting}[language=Caml]
# let register_heartbeat t hb =
    { t with last_heartbeat_time   = hb.Heartbeat.time;
             last_heartbeat_status = hb.Heartbeat.status_message;
  }
val register_heartbeat : client_info -> Heartbeat.t -> client_info = <fun>
\end{lstlisting}

\hypertarget{mutable-fields}{%
\subsection{Mutable Fields}\label{mutable-fields}}

Like most OCaml values, records are immutable by default. You can,
however, declare individual record fields as mutable. In the following
code, we've made the last two fields of
\passthrough{\lstinline!client\_info!} mutable:\index{mutable
record fields}\index{records/mutable fields in}

\begin{lstlisting}[language=Caml]
# type client_info =
    { addr: Unix.Inet_addr.t;
      port: int;
      user: string;
      credentials: string;
      mutable last_heartbeat_time: Time_ns.t;
      mutable last_heartbeat_status: string;
  }
type client_info = {
  addr : Unix.inet_addr;
  port : int;
  user : string;
  credentials : string;
  mutable last_heartbeat_time : Time_ns.t;
  mutable last_heartbeat_status : string;
}
\end{lstlisting}

The \passthrough{\lstinline!<-!} operator is used for setting a mutable
field. The side-effecting version of
\passthrough{\lstinline!register\_heartbeat!} would be written as
follows:

\begin{lstlisting}[language=Caml]
# let register_heartbeat t hb =
    t.last_heartbeat_time   <- hb.Heartbeat.time;
    t.last_heartbeat_status <- hb.Heartbeat.status_message
val register_heartbeat : client_info -> Heartbeat.t -> unit = <fun>
\end{lstlisting}

Note that mutable assignment, and thus the \passthrough{\lstinline!<-!}
operator, is not needed for initialization because all fields of a
record, including mutable ones, are specified when the record is
created.

OCaml's policy of immutable-by-default is a good one, but imperative
programming is an important part of programming in OCaml. We go into
more depth about how (and when) to use OCaml's imperative features in
\href{guided-tour.html\#imperative-programming}{Imperative Programming}.

\hypertarget{first-class-fields}{%
\subsection{First-Class Fields}\label{first-class-fields}}

Consider the following function for extracting the usernames from a list
of \passthrough{\lstinline!Logon!}
messages:\index{fields/first-class fields}\index{first-class
fields}\index{records/first-class fields in}

\begin{lstlisting}[language=Caml]
# let get_users logons =
    List.dedup_and_sort ~compare:String.compare
  (List.map logons ~f:(fun x -> x.Logon.user))
val get_users : Logon.t list -> string list = <fun>
\end{lstlisting}

Here, we wrote a small function
\passthrough{\lstinline!(fun x -> x.Logon.user)!} to access the
\passthrough{\lstinline!user!} field. This kind of accessor function is
a common enough pattern that it would be convenient to generate it
automatically. The \passthrough{\lstinline!ppx\_fields\_conv!} syntax
extension that ships with \passthrough{\lstinline!Core!} does just
that.\index{record field accessor functions}

The \passthrough{\lstinline![@@deriving fields]!} annotation at the end
of the declaration of a record type will cause the extension to be
applied to a given type declaration. So, for example, we could have
defined \passthrough{\lstinline!Logon!} as follows:

\begin{lstlisting}[language=Caml]
# module Logon = struct
    type t =
      { session_id: string;
        time: Time_ns.t;
        user: string;
        credentials: string;
      }
    [@@deriving fields]
  end
module Logon :
  sig
    type t = {
      session_id : string;
      time : Time_ns.t;
      user : string;
      credentials : string;
    }
    val credentials : t -> string
    val user : t -> string
    val time : t -> Time_ns.t
    val session_id : t -> string
    module Fields :
      sig
        val names : string list
        val credentials :
          ([< `Read | `Set_and_create ], t, string) Field.t_with_perm
        val user :
          ([< `Read | `Set_and_create ], t, string) Field.t_with_perm
        val time :
          ([< `Read | `Set_and_create ], t, Time_ns.t) Field.t_with_perm
...
      end
  end
\end{lstlisting}

Note that this will generate \emph{a lot} of output because
\passthrough{\lstinline!fieldslib!} generates a large collection of
helper functions for working with record fields. We'll only discuss a
few of these; you can learn about the remainder from the documentation
that comes with \passthrough{\lstinline!fieldslib!}.

One of the functions we obtain is \passthrough{\lstinline!Logon.user!},
which we can use to extract the user field from a logon message:

\begin{lstlisting}[language=Caml]
# let get_users logons =
    List.dedup_and_sort ~compare:String.compare
  (List.map logons ~f:Logon.user)
val get_users : Logon.t list -> string list = <fun>
\end{lstlisting}

In addition to generating field accessor functions,
\passthrough{\lstinline!fieldslib!} also creates a submodule called
\passthrough{\lstinline!Fields!} that contains a first-class
representative of each field, in the form of a value of type
\passthrough{\lstinline!Field.t!}. The \passthrough{\lstinline!Field!}
module provides the following functions:\index{Field
module/Field.setter}\index{Field module/Field.fset}\index{Field
module/Field.get}\index{Field
module/Field.name}\index{fieldslib}

\begin{description}
\tightlist
\item[\texttt{Field.name}]
Returns the name of a field
\item[\texttt{Field.get}]
Returns the content of a field
\item[\texttt{Field.fset}]
Does a functional update of a field
\item[\texttt{Field.setter}]
Returns \passthrough{\lstinline!None!} if the field is not mutable or
\passthrough{\lstinline!Some f!} if it is, where
\passthrough{\lstinline!f!} is a function for mutating that field
\end{description}

A \passthrough{\lstinline!Field.t!} has two type parameters: the first
for the type of the record, and the second for the type of the field in
question. Thus, the type of
\passthrough{\lstinline!Logon.Fields.session\_id!} is
\passthrough{\lstinline!(Logon.t, string) Field.t!}, whereas the type of
\passthrough{\lstinline!Logon.Fields.time!} is
\passthrough{\lstinline!(Logon.t, Time.t) Field.t!}. Thus, if you call
\passthrough{\lstinline!Field.get!} on
\passthrough{\lstinline!Logon.Fields.user!}, you'll get a function for
extracting the \passthrough{\lstinline!user!} field from a
\passthrough{\lstinline!Logon.t!}:

\begin{lstlisting}[language=Caml]
# Field.get Logon.Fields.user
- : Logon.t -> string = <fun>
\end{lstlisting}

Thus, the first parameter of the \passthrough{\lstinline!Field.t!}
corresponds to the record you pass to \passthrough{\lstinline!get!}, and
the second parameter corresponds to the value contained in the field,
which is also the return type of \passthrough{\lstinline!get!}.

The type of \passthrough{\lstinline!Field.get!} is a little more
complicated than you might naively expect from the preceding one:

\begin{lstlisting}[language=Caml]
# Field.get
- : ('b, 'r, 'a) Field.t_with_perm -> 'r -> 'a = <fun>
\end{lstlisting}

The type is \passthrough{\lstinline!Field.t\_with\_perm!} rather than
\passthrough{\lstinline!Field.t!} because fields have a notion of access
control that comes up in some special cases where we expose the ability
to read a field from a record, but not the ability to create new
records, and so we can't expose functional updates.

We can use first-class fields to do things like write a generic function
for displaying a record field:

\begin{lstlisting}[language=Caml]
# let show_field field to_string record =
    let name = Field.name field in
    let field_string = to_string (Field.get field record) in
    name ^ ": " ^ field_string
val show_field :
  ('a, 'b, 'c) Field.t_with_perm -> ('c -> string) -> 'b -> string = <fun>
\end{lstlisting}

This takes three arguments: the \passthrough{\lstinline!Field.t!}, a
function for converting the contents of the field in question to a
string, and a record from which the field can be grabbed.

Here's an example of \passthrough{\lstinline!show\_field!} in action:

\begin{lstlisting}[language=Caml]
# let logon = { Logon.
                session_id = "26685";
                time = Time_ns.of_string "2017-07-21 10:11:45 EST";
                user = "yminsky";
                credentials = "Xy2d9W"; }
val logon : Logon.t =
  {Logon.session_id = "26685"; time = 2017-07-21 17:11:45.000000+02:00;
   user = "yminsky"; credentials = "Xy2d9W"}
# show_field Logon.Fields.user Fn.id logon
- : string = "user: yminsky"
# show_field Logon.Fields.time Time_ns.to_string logon
- : string = "time: 2017-07-21 17:11:45.000000+02:00"
\end{lstlisting}

As a side note, the preceding example is our first use of the
\passthrough{\lstinline!Fn!} module (short for ``function''), which
provides a collection of useful primitives for dealing with functions.
\passthrough{\lstinline!Fn.id!} is the identity function.

\passthrough{\lstinline!fieldslib!} also provides higher-level
operators, like \passthrough{\lstinline!Fields.fold!} and
\passthrough{\lstinline!Fields.iter!}, which let you walk over the
fields of a record. So, for example, in the case of
\passthrough{\lstinline!Logon.t!}, the field iterator has the following
type:

\begin{lstlisting}[language=Caml]
# Logon.Fields.iter
- : session_id:(([< `Read | `Set_and_create ], Logon.t, string)
                Field.t_with_perm -> unit) ->
    time:(([< `Read | `Set_and_create ], Logon.t, Time_ns.t)
          Field.t_with_perm -> unit) ->
    user:(([< `Read | `Set_and_create ], Logon.t, string) Field.t_with_perm ->
          unit) ->
    credentials:(([< `Read | `Set_and_create ], Logon.t, string)
                 Field.t_with_perm -> unit) ->
    unit
= <fun>
\end{lstlisting}

This is a bit daunting to look at, largely because of the access control
markers, but the structure is actually pretty simple. Each labeled
argument is a function that takes a first-class field of the necessary
type as an argument. Note that \passthrough{\lstinline!iter!} passes
each of these callbacks the \passthrough{\lstinline!Field.t!}, not the
contents of the specific record field. The contents of the field,
though, can be looked up using the combination of the record and the
\passthrough{\lstinline!Field.t!}.

Now, let's use \passthrough{\lstinline!Logon.Fields.iter!} and
\passthrough{\lstinline!show\_field!} to print out all the fields of a
\passthrough{\lstinline!Logon!} record:

\begin{lstlisting}[language=Caml]
# let print_logon logon =
    let print to_string field =
      printf "%s\n" (show_field field to_string logon)
    in
    Logon.Fields.iter
      ~session_id:(print Fn.id)
      ~time:(print Time_ns.to_string)
      ~user:(print Fn.id)
      ~credentials:(print Fn.id)
val print_logon : Logon.t -> unit = <fun>
# print_logon logon
session_id: 26685
time: 2017-07-21 17:11:45.000000+02:00
user: yminsky
credentials: Xy2d9W
- : unit = ()
\end{lstlisting}

One nice side effect of this approach is that it helps you adapt your
code when the fields of a record change. If you were to add a field to
\passthrough{\lstinline!Logon.t!}, the type of
\passthrough{\lstinline!Logon.Fields.iter!} would change along with it,
acquiring a new argument. Any code using
\passthrough{\lstinline!Logon.Fields.iter!} won't compile until it's
fixed to take this new argument into account.

Field iterators are useful for a variety of record-related tasks, from
building record-validation functions to scaffolding the definition of a
web form from a record type. Such applications can benefit from the
guarantee that all fields of the record type in question have been
considered.
